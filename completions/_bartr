#compdef _bartr bartr

# main function - called via the compdef definition
# this handles the top level commands
# "sub-commands" are handled in different functions
# this keeps the code clean and makes auto-generation simple
# some of the functions are short enough to inline
function _bartr(){
  local line state

  # the first command will parse into "cmds"
  # sub-commands will parse into "args"
  # use a case statement to handle
  # every command has --help option
  # first command has --version option
  _arguments \
    '--help[Display help and usage]' \
    '--version[Display version info]' \
    '1: :->cmds' \
    '*::arg:->args'

  case $state in
    # define the top level commands (app, ls, new)
    # "commands" is a "header" that gets displayed
    # you can change the value
    cmds)
      _values \
        "commands" \
        "app[App commands]" \
        "ls[List the apps]" \
        "new[Create a new app]"
    ;;

    # call the sub-command functions
    # the function names use "breadcrumb" naming to avoid collision
    args)
      case $line[1] in
        app)
          _bartr_app
        ;;

        new)
          _bartr_new
        ;;

        # no sub-commands
        # every command ends with --help option
        ls)
          _arguments \
            '--help[Display help and usage]'
          return 0
        ;;
      esac
    ;;
  esac
}

# handle the "app" command
# same structure in all "handlers"
function _bartr_app(){
  local line state

  _arguments \
    '--help[Display help and usage]' \
    '1: :->cmds' \
    '*::arg:->args'

  case $state in
    # define "sub-commands"
    # bartr app (create info set)
    cmds)
      _values \
        'application commands' \
        'create[Create a new app]' \
        'info[Get application info]' \
        'set[Set application values]'
    ;;

    # handle the "bartr app" subcommands
    args)
    case $line[1] in
      # add options to create and set sub-commands
      create|set)
        _arguments \
        '--help[Display help and usage]' \
        '--app-name+[Application name]:App name:' \
        '--namespace+[Kubernetes namespace]:Namespace:' \
        '--image+[Image name]:Image and tag:' \
        '--cpu-limit+[CPU Limit]:CPU Limit:(250m 500m 1000m 2000m)' \
        '--memory-limit+[Memory Limit]:Memory Limit:(256Mi 512Mi 1024Mi 2048Mi)' \
        '--cpu-request+[CPU Request]:CPU Request:(250m 500m 1000m 2000m)' \
        '--memory-request+[Memory Request]:Memory Request:(256Mi 512Mi 1024Mi 2048Mi)' \
        '--readiness-probe+[Readiness Probe path]:Readiness Probe:(/readyz /healthz)' \
        '--liveness-probe+[Liveness Probe path]:Liveness Probe:(/healthz)' \
        '--port+[Port app exposes]:Port:(80 8080 8088)' \
        '--node-port+[NodePort for app]:Node Port:(30000 30080 30088 31000 31080 32000 32080)' \
        '--force[Force overwriting config]'
      ;;

      info)
        _arguments \
          '--help[Display help and usage]'
      ;;
    esac
    ;;
  esac
}

function _bartr_new(){
  local line state

  _arguments \
    '--help[Display help and usage]' \
    '1: :->cmds' \
    '*::arg:->args'

  case $state in
    # define "sub-commands"
    # bartr new (dotnet spring ts)
    cmds)
      _values \
        'select framework' \
        'dotnet[New dotnet app]' \
        'spring[New SpringBoot app]' \
        'ts[New TypeScript app]' \
    ;;

    args)
      case $line[2] in
        daemon|webapi)
          _arguments \
            '--help[Display help and usage]'
        ;;

        *)
          case $line[1] in
            # define "sub-commands"
            # bartr new (dotnet spring ts) (daemon webapi)
            dotnet|spring|ts)
            _values \
              'select app type' \
              'webapi[New WebAPI app]' \
              'daemon[New daemon app]'
          ;;
          esac
        ;;
      esac
    ;;
  esac
}
